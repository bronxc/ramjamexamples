filler:
	add.w #4,addx           ;xszog novelese
	and.w #$1ff,addx   
	add.w #6,addy           ;yszog novelese    
	and.w #$1ff,addy
	add.w #4,addz           ;zszog novelese        
	and.w #$1ff,addz
	lea coords(pc),a0        ;kocka pontjainak x,y,z koordinatai
	lea sinus(pc),a1         ;sinus ertekek
	lea $80(a1),a3           ;cosinus ertekek
	lea tarolo(pc),a2        ;2D x,y letarolasa
	lea addx(pc),a4          ;szogek
	moveq #61,d7              ;
h3	movem.w (a0)+,d0/d1/d6   ;x,y,z
	move.w 4(a4),d5          ;Zszog
	move.w (a3,d5.w),d4     ;cos ertek
	move.w (a1,d5.w),d5     ;sin ertek
	move.w d0,d2            ;c1
	move.w d1,d3		;c2
	muls d4,d0		;c1*cos(c0angle)
	muls d5,d1		;c2*sin(c0angle)
	muls d4,d3		;c2*cos(c0angle)
	muls d5,d2		;c1*sin(c0angle)
	sub.l d1,d0		;c1*cos(c0angle)-c2*sin(c0angle)
	add.l d2,d3		;c2*cos(c0angle)+c1*sin(c0angle)
	add.l d0,d0		
	add.l d3,d3
	swap d0                 ;uj c1
	swap d3                 ;uj c2
	move.w d6,d1		
	move.w d0,d6             ;X tarolasa
	move.w d3,d0             ;Y
	move.w (a4),d5           ;Xszog
	move.w (a3,d5.w),d4     ;cos ertek
	move.w (a1,d5.w),d5     ;sin ertek
	move.w d0,d2            ;c1
	move.w d1,d3		;c2
	muls d4,d0		;c1*cos(c0angle)
	muls d5,d1		;c2*sin(c0angle)
	muls d4,d3		;c2*cos(c0angle)
	muls d5,d2		;c1*sin(c0angle)
	sub.l d1,d0		;c1*cos(c0angle)-c2*sin(c0angle)
	add.l d2,d3		;c2*cos(c0angle)+c1*sin(c0angle)
	add.l d0,d0		
	add.l d3,d3
	swap d0                 ;uj c1
	swap d3                 ;uj c2
	move.w d6,d1		
	move.w d0,d6             ;uj Y tarolasa
	move.w d3,d0             ;Z
	move.w 2(a4),d5          ;Yszog
	move.w (a3,d5.w),d4     ;cos ertek
	move.w (a1,d5.w),d5     ;sin ertek
	move.w d0,d2            ;c1
	move.w d1,d3		;c2
	muls d4,d0		;c1*cos(c0angle)
	muls d5,d1		;c2*sin(c0angle)
	muls d4,d3		;c2*cos(c0angle)
	muls d5,d2		;c1*sin(c0angle)
	sub.l d1,d0		;c1*cos(c0angle)-c2*sin(c0angle)
	add.l d2,d3		;c2*cos(c0angle)+c1*sin(c0angle)
	add.l d0,d0		
	add.l d3,d3
	swap d0                 ;uj c1
	swap d3                 ;uj c2
	move.w d6,d1		
	exg d0,d3                ;X csere Z-vel    Z=d3
	moveq #11,d6             ;lekepezes 2D-re
	move.w #$660,d2
	sub.w d3,d2              ;$444-Z
	muls d2,d0               ;z'*x
	asr.l d6,d0              ;z'*x/2048=2D X
	muls d2,d1               ;z'*y
	asr.l d6,d1              ;z'*y/2048=2D Y
	add.w #150,d0		 ;X=X+150     kozepre a kepernyon           
	add.w #100,d1            ;Y=Y+100
	move.w d0,(a2)+		 ;2D X tarolasa
	move.w d1,(a2)+          ;2D Y tarolasa
	dbf d7,h3                ;8-szor               

